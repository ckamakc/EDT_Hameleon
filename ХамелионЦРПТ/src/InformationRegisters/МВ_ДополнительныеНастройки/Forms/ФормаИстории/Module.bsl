// ФТО:ALNA 22.07.2019 30701

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Настройка = Параметры.МВ_Настройка;
	Организация = Параметры.МВ_Организация;
	
	Заголовок = "" + Настройка + ?(ЗначениеЗаполнено(Организация), " (" + Организация + ")", " (общая)");
	
	ПолеНастройка = Новый ПолеКомпоновкиДанных("Настройка");
	ПолеОрганизация = Новый ПолеКомпоновкиДанных("Организация");
	
	ЭлементыОтбора = Список.КомпоновщикНастроек.Настройки.Отбор.Элементы;
	Для Каждого Элемент Из ЭлементыОтбора Цикл
		
		Если Элемент.РежимОтображения <> РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный Тогда
			Продолжить;
		КонецЕсли;
		
		Если Элемент.ЛевоеЗначение = ПолеНастройка Тогда
			Элемент.ПравоеЗначение = Настройка;
		ИначеЕсли Элемент.ЛевоеЗначение = ПолеОрганизация Тогда
			Элемент.ПравоеЗначение = Организация;
		КонецЕсли;
		
	КонецЦикла;
	
	Элементы.Значение.Формат = РегистрыСведений.МВ_ДополнительныеНастройки.ФорматнаяСтрокаНастройки(Настройка);
	
	//++ ФТО:SOAL 03.10.2019 32239
	Элементы.Значение.РежимПароля = РегистрыСведений.МВ_ДополнительныеНастройки.ВключатьРежимПароляДляНастройки(Настройка); 
	//-- ФТО:SOAL 03.10.2019 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	ЗначенияЗаполнения = Новый Структура("Настройка, Организация", Настройка, Организация);
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("РегистрСведений.МВ_ДополнительныеНастройки.Форма.ФормаЗаписиИстории", ПараметрыОткрытияФормы, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)

	Отказ = Истина;
	
	ПоляКлюча = Новый Структура;
	ПоляКлюча.Вставить("Период", Элементы.Список.ТекущиеДанные.Период);
	ПоляКлюча.Вставить("Настройка", Настройка);
	ПоляКлюча.Вставить("Организация", Организация);
	
	МассивДляКонструктора = Новый Массив;
	МассивДляКонструктора.Добавить(ПоляКлюча);
	Ключ = Новый("РегистрСведенийКлючЗаписи.МВ_ДополнительныеНастройки", МассивДляКонструктора);
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("Ключ", Ключ);
	
	ОткрытьФорму("РегистрСведений.МВ_ДополнительныеНастройки.Форма.ФормаЗаписиИстории", ПараметрыОткрытияФормы, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПослеУдаления(Элемент)
	
	Оповестить("МВ_ДополнительныеНастройки.ИзменениеНастроек");

КонецПроцедуры

#КонецОбласти